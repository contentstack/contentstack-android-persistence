import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'com.android.library'
    id "realm-android"
    id "com.vanniktech.maven.publish" version "0.33.0"
}

android {
    compileSdkVersion 34
    namespace 'com.contentstack.sdk.persistence'
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1
        versionName "0.0.2"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'com.contentstack.sdk:android:4.0.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.7.0'

    // Security fixes for transitive dependencies
    // Fix CVE-2022-25647 (Deserialization vulnerability in Gson)
    implementation 'com.google.code.gson:gson:2.10.1'
    // Fix CVE-2020-29582 (Information Exposure in kotlin-stdlib)
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.1.0'
}

// Force all kotlin-stdlib versions to 2.1.0 (fixes CVE-2020-29582 from transitive deps like Realm)
configurations.all {
    resolutionStrategy {
        force 'org.jetbrains.kotlin:kotlin-stdlib:2.1.0'
        force 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:2.1.0'
        force 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.1.0'
    }
}

mavenPublishing {
    publishToMavenCentral("CENTRAL_PORTAL")
    
    coordinates(GROUP, POM_ARTIFACT_ID, VERSION_NAME)
    
    pom {
        name = POM_NAME
        description = POM_DESCRIPTION
        inceptionYear = POM_INCEPTION_YEAR
        url = POM_URL
        licenses {
            license {
                name = POM_LICENSE_NAME
                url = POM_LICENSE_URL
                distribution = POM_LICENSE_DIST
            }
        }
        developers {
            developer {
                id = POM_DEVELOPER_ID
                name = POM_DEVELOPER_NAME
                url = POM_DEVELOPER_URL
            }
        }
        scm {
            url = POM_SCM_URL
            connection = POM_SCM_CONNECTION
            developerConnection = POM_SCM_DEV_CONNECTION
        }
    }
}
